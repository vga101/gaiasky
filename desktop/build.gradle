apply plugin: "java"
apply plugin: "com.install4j.gradle"

sourceCompatibility = 1.7
sourceSets.main.java.srcDirs = ["src/"]

project.ext.mainClassName = "gaia.cu9.ari.gaiaorbit.desktop.GaiaSkyDesktop"
project.ext.assetsDir = new File("../android/assets");
project.ext.workDir = new File(".");

install4j {
    installDir = file('/home/tsagrista/Programs/install4j6')
}

sourceSets {
    main {
        main {
            output.resourcesDir = project.assetsDir.absolutePath
        }
    }
}

task run(dependsOn: jar, type: JavaExec, description: "Runs the desktop project") {
    main = project.mainClassName
    systemProperties['properties.file'] = ''
    systemProperties['assets.location'] = '../android/assets/'
    	
    // Parallel GC
    //jvmArgs = ['-Xms2g', '-Xmx4g', '-XX:+UseParallelGC']
    // CMS GC
    jvmArgs = ['-Xms3g', '-Xmx4g', '-XX:+UseConcMarkSweepGC']
    // G1 GC
    //jvmArgs = ['-Xms2g', '-Xmx4g', '-XX:+UseG1GC']
    classpath = sourceSets.main.runtimeClasspath
    classpath += files(project.assetsDir)
    standardInput = System.in
    workingDir = project.workDir
    ignoreExitValue = true
}

task runCatalogFilter(dependsOn: jar, type: JavaExec, description: "Runs the catalog filter application to generate day catalogs for WebGL") {
    main = 'gaia.cu9.ari.gaiaorbit.data.GaiaCatalogFilter'
    classpath = sourceSets.main.runtimeClasspath
    classpath += files(project.assetsDir)
    standardInput = System.in
    workingDir = project.workDir
    ignoreExitValue = true
}


task runOctreeGenerator(dependsOn: jar, type: JavaExec, description: "Runs the octree generator to create the octree structure from a base catalog") {
    main = 'gaia.cu9.ari.gaiaorbit.data.OctreeGeneratorTest'
    systemProperties['properties.file'] = '../android/assets/conf/global.properties'
    classpath = sourceSets.main.runtimeClasspath
    classpath += files(project.assetsDir)
    standardInput = System.in
    workingDir = project.workDir
    ignoreExitValue = true
}

task runGalaxyGenerator(dependsOn: jar, type: JavaExec, description: "Runs the galaxy generator to procedurally generate a galaxy") {
    main = 'gaia.cu9.ari.gaiaorbit.data.galaxy.GalaxyGenerator'
    systemProperties['properties.file'] = '../android/assets/conf/global.properties'
    classpath = sourceSets.main.runtimeClasspath
    classpath += files(project.assetsDir)
    standardInput = System.in
    workingDir = project.workDir
    ignoreExitValue = true
}

task createDistDir (dependsOn: ':versionFile', description: "Creates the releases/dist folder") << {
    File distDir = new File(project.distDir)
    // Prepare output directory
    delete distDir
    mkdir distDir

}

task copyToLib (dependsOn: [compileJava, createDistDir], description: "Copies the runtime dependencies to releases/dist/lib") << {

    // Create releases/dist/lib
    File distLib = new File(project.distDir, 'lib')
    mkdir distLib
    project.ext.distLib = distLib.getPath()

    // Copy deps to dist lib folder
    ['runtime'].each { mode ->
        copy {
            from configurations.getByName(mode).files
            into project.distLib
            exclude "**/core-1.0.jar"
            exclude "**/core-" + version + ".jar"
        }
    }

    // Remove folders
    delete project.distLib + '/conf'
    delete project.distLib + '/data'
    delete project.distLib + '/font'
    delete project.distLib + '/i18n'
    delete project.distLib + '/icon'
    delete project.distLib + '/img'
    delete project.distLib + '/music'
    delete project.distLib + '/scripts'
    delete project.distLib + '/shader'
    delete project.distLib + '/shaders'
    delete project.distLib + '/skins'
    delete project.distLib + '/postprocessing-sources.jar'

}

task gaiaskyJar (dependsOn: [compileJava, jar, copyToLib],  description: "Creates the gaiasky jar and puts it to the dist folder") << {
    copy {
        from "$baseDir/desktop/build/libs/desktop-" + version + ".jar"
        into "$distLib/"
    }
    file("$distLib/desktop-" + version + ".jar").renameTo(file("$distLib/gaiasky.jar"))
}

task dist (dependsOn: gaiaskyJar, description: "Compiles the project, creates the jar and moves it all to releases/gaiasky-\$tag-\$rev") << {
    // Copy conf
    mkdir "$distDir/conf"
    copy {
        from "$baseDir/android/assets/conf"
        into "$distDir/conf"
        exclude "**/android"
    }

    // Copy data
    mkdir "$distDir/data"
    copy {
        from "../android/assets/data"
        into "$distDir/data"
        exclude "**/android"
		// exclude "**/octree"
		exclude "**/tgas_201507"
		exclude "**/tgas_final"

        exclude "**/earth-8k.jpg"
        exclude "**/earth-night-2k.jpg"

		exclude "**/catalog.txt"
        exclude "**/*-wgl.json"

    }

    // Add icons to data
    copy {
        from "data"
        into "$distDir/data"
        include "**/config/**"
    }

    // Copy i18n files
    mkdir "$distDir/i18n"
    copy {
        from "$baseDir/android/assets/i18n"
        into "$distDir/i18n"
        exclude "**/android/"
    }
    
    // Copy music
    mkdir "$distDir/music"
    copy{
    	from "../android/assets/music"
    	into "$distDir/music"
    }

    // Copy script files
    mkdir "$distDir/scripts"
    copy {
        from "$baseDir/android/assets/scripts"
        into "$distDir/scripts"
    }

    // Copy scripts, README, LICENSE and AUTHORS
    copy {
        from "exe"
        into "$distDir"
        exclude "*.sh"
    }
    copy {
        from "$baseDir/"
        into "$distDir"
        include "README.md"
        include "LICENSE"
        include "AUTHORS"
    }
    copy {
        from "$baseDir/android/assets/icon"
        into "$distDir"
        include "ic_launcher.png"
        include "ic_launcher.ico"
        include "ic_launcher.svg"
    }

    // Make gaiasky script executable
    def cmd = "chmod u+x $distDir/gaiasky"
    cmd.execute()

    // Replace version in readme file
    project.ext.versionFile = file("../core/build/classes/main/version").text
    ant.replace(file: "$distDir/README.md", token: "--------------------------", value: "$versionFile")


}
dist.dependsOn classes

task createTar(dependsOn: dist, description: "Creates application tar package") << {
    File packageDir = new File(project.packageDir)

    delete packageDir
    mkdir packageDir

    ant.tar(destfile: "$packageDir/" + project.distName + ".tar.gz", compression: 'gzip', longfile: 'gnu') {
        tarfileset(dir: "$releasesDir") {
        	include (name: "$distName/**")
            exclude (name: "$distName/gaiasky")
        }
        tarfileset(dir: "$releasesDir", filemode:'755') {
            include(name: "$distName/gaiasky")
        }
    }

}

task createArch(dependsOn: createTar, description: "Copies Arch Linux AUR build scripts PKGBUILD and gaiasky.install and replaces version and md5") << {
	File archDir = new File(project.packageDir + "/arch/")
	mkdir archDir
	copy {
		from "installerscripts/arch"
		into "$packageDir/arch"
	}
	
	File tarFile = new File("$packageDir/" + project.distName + ".tar.gz")
	ant.checksum(file: tarFile, algorithm: "md5", todir: "$packageDir/")
	
	String md5Tar = new File("$packageDir/" + project.distName + ".tar.gz.md5").text
	md5Tar = md5Tar.replace("\n", "").replace("\r", "");
	
	ant.replace(file: "$packageDir/arch/PKGBUILD", token: '@version.revision@', value: "$tagRev")
	ant.replace(file: "$packageDir/arch/PKGBUILD", token: '@md5checksum@', value: md5Tar)
	
	copy {
		from "$baseDir/CHANGELOG.md"
		into "$packageDir/arch/"
	}
	
	// Run makepkg to create .SRCINFO
	//exec {
    // 	executable "makepkg"
    //	args "--printsrcinfo",">","$packageDir/arch/.SRCINFO"
    //}
}

def buildTime() {
    return new Date()
}

def distDirSize() {
	File dist = new File(project.distDir)
	return Math.round(dist.directorySize() / 1024)
}

task createDeb(dependsOn: createArch, description: "Creates the .deb package") << {
	// Base deb directory
	File debDir = new File(project.packageDir + "/$distName")
	// Opt dir
	File debOptDir = new File(project.packageDir + "/$distName/opt/gaiasky")
	mkdir debOptDir
	// DEBIAN directory
	File debDEBIANDir = new File(project.packageDir + "/$distName/DEBIAN")
	mkdir debDEBIANDir
	
	copy {
		from "installerscripts/debian"
		into "$packageDir/$distName/DEBIAN"
	}
	
	ant.replace(file: "$packageDir/$distName/DEBIAN/control", token: '@version.revision@', value: "$tagRev")
	ant.replace(file: "$packageDir/$distName/DEBIAN/control", token: '@installedsize@', value: "${distDirSize()}")
	ant.replace(file: "$packageDir/$distName/DEBIAN/control", token: '@date@', value: "${buildTime()}")
	
	copy {
		from "$distDir"
		into "$packageDir/$distName/opt/gaiasky/"
	}
	
	// Create deb
	exec {
     	executable "dpkg-deb"
    	args "--build","$packageDir/$distName"
    }
    
    // Remove temp dir directory
    delete debDir
    
}

task prepareInstall4jScript (dependsOn: createArch, description: "Prepares the install4j script and copies it to the packages folder") << {
    copy {
        from "installerscripts/template.install4j"
        into "$packageDir"
    }
    file("$packageDir/template.install4j").renameTo(file("$packageDir/" + project.distName + ".install4j"))

    project.ext.releaseFolder = "$baseDir/releases/$distName"

    ant.replace(file: "$packageDir/" + project.distName + ".install4j", token: '@gs-release-folder@', value: "$distDir")
    ant.replace(file: "$packageDir/" + project.distName + ".install4j", token: '@gs-git-folder@', value: "$baseDir")
    ant.replace(file: "$packageDir/" + project.distName + ".install4j", token: '@version-tag@', value: "$tagRev")
}

task install4jMedia(type: com.install4j.gradle.Install4jTask, description: "Runs the install4j task to create the installers") {
    dependsOn 'prepareInstall4jScript' // example task that prepares the distribution for install4j

    projectFile = file("$packageDir/" + project.distName + ".install4j")
    variables = [majorVersion: project.tag, build: project.rev]
}


task pack (dependsOn: install4jMedia, description: "Creates the releases for windows, mac, linux and the tar package") << {

}

eclipse {
    project {
        name = appName + "-desktop"
        linkedResource name: 'assets', type: '2', location: 'PARENT-1-PROJECT_LOC/android/assets'
    }
}

task afterEclipseImport(description: "Post processing after project generation", group: "IDE") {
    doLast {
        def classpath = new XmlParser().parse(file(".classpath"))
        new Node(classpath, "classpathentry", [ kind: 'src', path: 'assets' ]);
        def writer = new FileWriter(file(".classpath"))
        def printer = new XmlNodePrinter(new PrintWriter(writer))
        printer.setPreserveWhitespace(true)
        printer.print(classpath)
    }
}
