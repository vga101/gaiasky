buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "http://maven.ej-technologies.com/repository" }
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
        flatDir {
            dirs 'lib'
        }
    }
    dependencies {
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
        classpath "gradle.plugin.install4j.install4j:gradle_plugin:6.1.2"
        //classpath "com.android.tools.build:gradle:2.3.1"
    }

}

plugins {
  id "com.install4j.gradle" version "6.1.2"
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    version = '1.0'

    ext {
        appName = 'GaiaSky'
        gdxVersion = '1.9.6'
        roboVMVersion = '1.4.0'
        box2DLightsVersion = '1.3'
        ashleyVersion = '1.4.0'
        aiVersion = '1.5.0'
		jythonVersion = '2.5.3'
        jcommanderVersion = '1.64'
        googleanalyticsjavaVersion = '1.1.2'
        slf4jVersion = '1.7.25'
    }

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }

    eclipse {
        classpath {
           downloadSources=true
        }
    }
}

project(":desktop") {
    apply plugin: "java"

    def cmd = "git describe --abbrev=0 --tags HEAD"
    def proc = cmd.execute()
    project.ext.tag = proc.text.trim()

    cmd = "git rev-parse --short HEAD"
    proc = cmd.execute()
    project.ext.rev = proc.text.trim()

    cmd = "uname -snmr"
    proc = cmd.execute()
    project.ext.system = proc.text.trim()

    cmd = "whoami"
    proc = cmd.execute()
    project.ext.builder = proc.text.trim()

    project.ext.buildtime = new Date()

    version = "$tag"

    println ""
    println "BUILD INFO FROM GIT"
    println "==================="
    println "git tag: $tag"
    println "git rev: $rev"
    println "buildtime: $buildtime"
    println "builder: $builder"
    println "system: $system"
    println ""


    // Set some build variables
    project.ext.baseDir = System.getProperty("user.dir")
    project.ext.tagRev = project.tag + "." + project.rev
    project.ext.distName = "gaiasky-$tagRev"
    project.ext.releasesDir = "$baseDir/releases"
    project.ext.distDir = "$releasesDir/$distName"
    project.ext.packageName = "packages-$tagRev"
    project.ext.packageDir = "$releasesDir/$packageName"

    println ""
    println "BUILD VARIABLES AND INFO"
    println "========================"
    println "base dir: $baseDir"
    println "tag.rev: $tagRev"
    println "dist name: $distName"
    println "dist dir: $distDir"
    println "packages name: $packageName"
    println "packages dir: $packageDir"
    println ""

    dependencies {
        compile project(":core")
        //compile "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-controllers-lwjgl3:$gdxVersion"
	compile "org.python:jython-standalone:$jythonVersion"
        compile "com.beust:jcommander:$jcommanderVersion"
        compile "com.brsanthu:google-analytics-java:$googleanalyticsjavaVersion"
        compile "org.slf4j:slf4j-nop:$slf4jVersion"
	compile files('../core/lib/postprocessing.jar',
			'../core/lib/jafama.jar',
			'../android/assets',
                        'lib/stil.jar')
    }

    jar {
        manifest {
            attributes 'Implementation-Title': 'Gaia Sky desktop',
                       'Implementation-Version': version
        }
        from('../android/assets') {
            include 'font/main-font.fnt'
            include 'font/main-font.png'
            include 'font/font2d.fnt'
            include 'font/font2d.png'
            include 'icon/ic_launcher.png'
            include 'img/**'
            exclude 'img/splash'
            include 'shader/**'
            include 'shaders/**'
            include 'skins/**'
            exclude 'skins/raw'
        }
        from('build/classes/main') {
            include '**'
        }
        from('../core/build/classes/main') {
            include '**'
        }


        println "Creating desktop jar file"
    }

}



project(":html") {
    apply plugin: "gwt"
    apply plugin: "war"

	configurations.all {
		resolutionStrategy {
			failOnVersionConflict()
			force 'com.google.gwt:gwt-user:2.7.0', 'com.google.gwt:gwt-dev:2.7.0'
		}
	}

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx:$gdxVersion:sources"
        compile "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion:sources"
        compile "com.badlogicgames.gdx:gdx-controllers:$gdxVersion:sources"
        compile "com.badlogicgames.gdx:gdx-controllers-gwt:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-controllers-gwt:$gdxVersion:sources"
    }
}

project(":core") {
    apply plugin: "java"

    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-controllers:$gdxVersion"
	compile "junit:junit:4.12"
	compile files('lib/postprocessing.jar',
			'lib/jafama.jar')
    }

    jar {
        manifest {
            attributes 'Implementation-Title': 'Gaia Sky core',
                       'Implementation-Version': version
        }
        println "Creating core jar file"
    }
}

project(":android") {
//    apply plugin: "android"
//
//    configurations { natives }
//
//    dependencies {
//        compile project(":core")
//        compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
//        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
//        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
//        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
//        compile "com.badlogicgames.gdx:gdx-controllers:$gdxVersion"
//		  compile "org.python:jython-standalone:$jythonVersion"
//        compile "com.badlogicgames.gdx:gdx-controllers-android:$gdxVersion"
//		  compile files('../core/lib/postprocessing.jar')
//    }
}

tasks.eclipse.doLast {
    delete ".project"
}



tasks.create('versionFile').doLast {
    File versionFile = file('core/build/classes/main/version')
    delete versionFile
    def writer = new FileWriter(versionFile)

    try {
       writer.write('buildtime=' + project(':desktop').buildtime + '\n')
       writer.write('build=' + project(':desktop').rev + '\n')
       writer.write('version=' + project(':desktop').tag + '\n')
       writer.write('builder=' + project(':desktop').builder + '\n')
       writer.write('system=' + project(':desktop').system + '\n')
       writer.flush()
    } finally {
       writer.close()
    }
}

task createWrapper(type: Wrapper) { gradleVersion = "3.2" }
